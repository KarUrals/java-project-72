plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'io.ebean' version '13.11.0'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {

    implementation 'io.javalin:javalin:4.3.0'   //фреймворк Javalin
    implementation 'org.slf4j:slf4j-simple:1.7.36'   //логер SLF4J

    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'   //шаблонизатор Thymeleaf
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.1.0'   //позволяет создавать общие макеты и переиспользовать их
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'   //добавляет в шаблоны объект temporals со множеством полезных методов по форматированию
    implementation 'org.webjars:bootstrap:5.2.3'   //фреймворк bootstrap

    implementation 'com.h2database:h2:2.1.210'
    implementation 'org.postgresql:postgresql:42.3.3'

    implementation 'io.ebean:ebean:12.15.1'   //фреймворк Ebean
    implementation 'io.ebean:ebean-ddl-generator:12.15.1'   //позволяет генерировать миграции
    implementation 'io.ebean:ebean-querybean:12.15.1'   //Query builder. Позволяет строить запросы
    implementation 'io.ebean:ebean-migration:12.15.1'   //позволяет автоматически выполнять миграции
    implementation 'io.ebean:ebean-annotation:7.7'   //добавляет различные аннотации
    annotationProcessor 'io.ebean:querybean-generator:12.15.1'   //обработчик аннотаций используется для генерации компонента запроса

    // NOTE: ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'com.konghq:unirest-java:3.13.11'
    implementation 'org.jsoup:jsoup:1.15.3'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'io.ebean:ebean-test:12.15.1'
}

test {
    useJUnitPlatform()
}

application {
    mainClass = 'hexlet.code.App'
}

compileJava {
    options.release = 17
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean